include(cmake/detect-features-c.cmake)
include(cmake/detect-features-cxx.cmake)
include(cmake/detect-features-fortran.cmake)

# Programs
find_program(SYNC sync)
if(SYNC)
    set(CMK_HAS_SYNC 1)
endif()


set(CMK_MACHINE_NAME \"${CHARM_PLATFORM}\")
set(CHARM_VERSION ${PROJECT_VERSION})
set(CMK_CCS_AVAILABLE 1)
if(${NETWORK} STREQUAL "uth" OR ${NETWORK} STREQUAL "pami" OR ${NETWORK} STREQUAL "pamilrts")
  set(CMK_CCS_AVAILABLE 0)
endif()

set(CMK_NO_PARTITIONS 0)
if(${NETWORK} STREQUAL "netlrts" OR ${NETWORK} STREQUAL "multicore" OR ${NETWORK} STREQUAL "uth" OR ${NETWORK} STREQUAL "pami" OR ${NETWORK} STREQUAL "shmem" OR ${NETWORK} STREQUAL "sim")
  set(CMK_NO_PARTITIONS 1)
endif()

set(CMK_HAS_OPENMP ${OPENMP_FOUND})

if(${CMAKE_CXX_COMPILER_ID} STREQUAL "AppleClang")
  # TODO: Apple clang needs external library for OpenMP support, disable for now.
  set(CMK_HAS_OPENMP 0)
endif()


# Misc. linker flags (mostly for Charm4py)
include(CheckCCompilerFlag)
set(CMAKE_REQUIRED_FLAGS "-Wl,--no-as-needed")
check_c_compiler_flag("" MY_CXX_NO_AS_NEEDED)
if(MY_CXX_NO_AS_NEEDED)
  set(CXX_NO_AS_NEEDED "-Wl,--no-as-needed")
else()
  set(CXX_NO_AS_NEEDED " ")
endif()

set(CMAKE_REQUIRED_FLAGS "-Wl,--whole-archive")
check_c_compiler_flag("" MY_LDXX_WHOLE_ARCHIVE_PRE)
if(MY_LDXX_WHOLE_ARCHIVE_PRE)
  set(LDXX_WHOLE_ARCHIVE_PRE "-Wl,--whole-archive")
else()
  set(LDXX_WHOLE_ARCHIVE_PRE " ")
endif()

if(${LDXX_WHOLE_ARCHIVE_PRE} STREQUAL " ")
    set(CMAKE_REQUIRED_FLAGS "-Wl,-all_load")
    # message("DOING  MY_LDXX_WHOLE_ARCHIVE_ALL_LOAD")
    check_c_compiler_flag("" MY_LDXX_WHOLE_ARCHIVE_ALL_LOAD)
    if(MY_LDXX_WHOLE_ARCHIVE_ALL_LOAD)
      # message("SUCCESS  MY_LDXX_WHOLE_ARCHIVE_ALL_LOAD")
      set(LDXX_WHOLE_ARCHIVE_PRE "-Wl,-all_load")
    else()
      # message("FAILED  MY_LDXX_WHOLE_ARCHIVE_ALL_LOAD")
      set(LDXX_WHOLE_ARCHIVE_PRE " ")
    endif()
    # message(FATAL_ERROR "x${MY_LDXX_WHOLE_ARCHIVE_ALL_LOAD}x${LDXX_WHOLE_ARCHIVE_PRE}y")
endif()

set(CMAKE_REQUIRED_FLAGS "-Wl,--no-whole-archive")
check_c_compiler_flag("" MY_LDXX_WHOLE_ARCHIVE_POST)
if(MY_LDXX_WHOLE_ARCHIVE_POST)
  set(LDXX_WHOLE_ARCHIVE_POST "-Wl,--no-whole-archive")
else()
  set(LDXX_WHOLE_ARCHIVE_POST " ")
endif()

set(CMAKE_REQUIRED_FLAGS "")

# Create conv-autoconfig.h by iterating over all variable names and #defining them.
get_cmake_property(_variableNames VARIABLES)
list (SORT _variableNames)

set(optfile ${CMAKE_BINARY_DIR}/include/conv-autoconfig.h)
file(REMOVE ${optfile})

foreach (v ${_variableNames})
    if(("${v}" MATCHES "^CMK_"  OR "${v}" MATCHES "^SIZEOF_" OR "${v}" MATCHES "^CHARM_") AND NOT "${v}" MATCHES "_CODE$")
        if("${${v}}" STREQUAL "" OR "${${v}}" STREQUAL "FALSE")
            set(${v} 0)
        elseif("${${v}}" STREQUAL "TRUE")
            set(${v} 1)
        endif()
        file(APPEND ${optfile} "#define ${v} ${${v}}\n" )
    elseif("${v}" MATCHES "^HAVE_")
        if("${${v}}" STREQUAL "" OR "${${v}}" STREQUAL "FALSE")
            set(${v} 0)
            file(APPEND ${optfile} "/* #define ${v} ${${v}} */\n" )
        elseif("${${v}}" STREQUAL "TRUE")
            set(${v} 1)
            file(APPEND ${optfile} "#define ${v} ${${v}}\n" )
        endif()
    endif()
endforeach()
